defpackage csv :
   import core
   import collections

defstruct UnclosedQuote <: Exception
defmethod print (o:OutputStream, e:UnclosedQuote) :
   print(o, "Unclosed opening quotation")

defstruct UnexpectedCharacter <: Exception : (char:Char)
defmethod print (o:OutputStream, e:UnexpectedCharacter) :
   print(o, "Unexpected Character: %~." % [char(e)])

public defstruct CSV :
   filename: String
   header: HashTable<String,Int>|True|False with: (setter => set-header)
   delim: Char
   newline: Char

public defn field (csv:CSV, t:Tuple, name:String) :
   t[get(header(csv) as HashTable<String,Int>, name)]

defn to-array<?T> (vec:Vector<?T>) -> Array<T> :
   val arr = Array<T>(length(vec))
   for i in 0 to length(vec) do :
      arr[i] = vec[i]
   arr

public defn parse (csv:CSV) -> Array<Tuple> :
   val file = FileInputStream(filename(csv))
   val records = Vector<Tuple>()
   var cur-char:Char|False = get-char(file)
   var cur-record = Vector<String>()
   var cur-field:String = ""

   while cur-char is Char :
      switch {cur-char == _} :
         delim(csv) :
            add(cur-record, cur-field)
            cur-field = ""
            cur-char = get-char(file)
         newline(csv) :
            add(cur-record, cur-field)
            add(records, to-tuple(cur-record))
            cur-char = get-char(file)
            cur-record = Vector<String>()
         '"' :
            val ret-tuple:[String, Char|False] = get-field(csv, file, false)
            cur-field = ret-tuple[0] as String
            cur-char = ret-tuple[1] as Char|False
         else :
            val ret-tuple:[String, Char|False] = get-field(csv, file, cur-char)
            cur-field = ret-tuple[0] as String
            cur-char = ret-tuple[1] as Char|False

   if header(csv) is True :
      val hdrs = HashTable<String,Int>()
      for i in 0 to length(records[0]) do :
         hdrs[records[0][i]] = i
      set-header(csv, hdrs)
      remove(records, 0 to 1)

   to-array(records)

defn get-field (csv:CSV, file:FileInputStream, unquoted:Char|False) -> [String, Char|False] :
   var field:String = ""
   label<[String, Char|False]> end-of-field :
      var current:Char|False
      var next:Char|False

      if unquoted is Char :
         current = unquoted as Char
      else :
         current = get-char(file)

      if unquoted is False :
         while current is Char :
            if current == '"' :
               next = get-char(file)
               if next == '"' :
                  field = append(field, to-string(current))
               else if next == delim(csv) or next == newline(csv) or next is False :
                  end-of-field([field, next])
               else :
                  throw(UnexpectedCharacter(next as Char))
            else if current is False :
               throw(UnclosedQuote())
            else :
               field = append(field, to-string(current))

            current = get-char(file)
         [field, current]
      else :
         while current is Char :
            if current == delim(csv) or current == newline(csv) :
               end-of-field([field, current])
            else if current == '"' :
               throw(UnexpectedCharacter(current as Char))
            else :
               field = append(field, to-string(current))

            current = get-char(file)
         [field, current]
